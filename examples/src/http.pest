// pest. The Elegant Parser
// 
// Licensed under the Apache License, Version 2.0
// <LICENSE-APACHE or http://www.apache.org/licenses/LICENSE-2.0> or the MIT
// license <LICENSE-MIT or http://opensource.org/licenses/MIT>, at your
// option. All files in the project carrying such notice may not be copied,
// modified, or distributed except according to those terms.
http = { SOI ~ (delimiter | request)* ~ EOI }

request = {
    request_line ~ headers? ~ NEWLINE
}

request_line = _{ method ~ " "+ ~ uri ~ " "+ ~ "HTTP/" ~ version ~ NEWLINE }
uri          =  { (!whitespace ~ ANY)+ }
method       =  { ("GET" | "DELETE" | "POST" | "PUT") }
version      =  { (ASCII_DIGIT | ".")+ }
whitespace   = _{ " " | "\t" }

headers      = { header+ }
header       = { header_name ~ ":" ~ whitespace ~ header_value ~ NEWLINE }
header_name  = { (!(NEWLINE | ":") ~ ANY)+ }
header_value = { (!NEWLINE ~ ANY)+ }

delimiter = { NEWLINE+ }
